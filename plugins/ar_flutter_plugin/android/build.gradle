plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
}

group 'io.carius.lars.ar_flutter_plugin'
version '1.0-SNAPSHOT'

repositories {
  google()
  mavenCentral()
}

android {
  namespace "io.carius.lars.ar_flutter_plugin"
  compileSdk 34

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
  defaultConfig {
    minSdk 24
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}


dependencies {
  implementation platform('org.jetbrains.kotlin:kotlin-bom')
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  implementation "com.google.ar:core:1.30.0"
  implementation 'com.google.ar.sceneform:core:1.17.1'
  implementation 'com.google.ar.sceneform:assets:1.17.1'
  implementation 'com.google.ar.sceneform.ux:sceneform-ux:1.17.1'
  implementation 'androidx.appcompat:appcompat:1.6.1'
}

afterEvaluate {
  def containsEmbeddingDependencies = false
  for (def configuration : configurations.all) {
    for (def dependency : configuration.dependencies) {
      if (dependency.group == 'io.flutter' &&
              dependency.name.startsWith('flutter_embedding') &&
              dependency.isTransitive())
      {
        containsEmbeddingDependencies = true
        break
      }
    }
  }
  if (!containsEmbeddingDependencies) {
    android {
      namespace "io.carius.lars.ar_flutter_plugin"

      dependencies {
        def lifecycle_version = "1.1.1"
        compileOnly "android.arch.lifecycle:runtime:$lifecycle_version"
        compileOnly "android.arch.lifecycle:common:$lifecycle_version"
        compileOnly "android.arch.lifecycle:common-java8:$lifecycle_version"
      }
    }
  }
}
